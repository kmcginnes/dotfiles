# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Documents/Projects

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
setopt extendedglob
typeset -U config_files
config_files=($ZSH/(^*.disabled/)#*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  echo "Loading Path: $file"
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  echo "Loading ZSH: $file"
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  echo "Loading Completion: $file"
  source $file
done

unset config_files
